.text
.global _main
.align 4

cnt_str: ; count bytes until quote character (0x22)
    mov x8, x0
cnt_str_loop:
    ldrb w9, [x8], #1
    cmp w9, #0x22
    b.ne cnt_str_loop
    sub x0, x8, x0
    ret

cnt_strnull: ; count bytes until null terminator
    mov x8, x0
cnt_strnull_loop:
    ldrb w9, [x8], #1
    cbz w9, cnt_str_loop
    sub x0, x8, x0
    ret

prep_str: ; replace quote with null terminator (prepare string for C functions)
    mov x8, x0
prep_str_loop:
    ldrb w9, [x8], #1
    cmp w9, #0x22
    b.ne prep_str_loop
    mov w9, #0
    strb w9, [x8, #-1]
    ret

fix_str: ; replace null terminator with quote (restore string format)
    mov x8, x0
fix_str_loop:
    ldrb w9, [x8], #1
    cbz w9, fix_str_fin
    b fix_str_loop
fix_str_fin:
    mov w9, #0x22
    strb w9, [x8, #-1]
    ret

do_input_byte: ; read single byte from stdin via getchar
    stp fp, lr, [sp, #-16]!
    bl _getchar 
    str x0, [x20], #8
    ldp fp, lr, [sp], #16
    ret

do_input_quad: ; read 8 bytes from stdin via read syscall
    stp fp, lr, [sp, #-16]!
    mov x0, #0
    mov x1, x20
    mov x2, #8
    bl _read 
    add x20, x20, #8
    ldp fp, lr, [sp], #16
    ret

do_pop_print: ; pop value from stack and print using printf
    ldr x1, [x20, #-8]!
    stp fp, lr, [sp, #-16]!
    str x1, [sp, #-16]!

    bl _printf 

    add sp, sp, #16
    ldp fp, lr, [sp], #16
    ret

_main: ; program entry point
    stp fp, lr, [sp, #-16]!
    stp x0, x1, [sp, #-16]!
    mov x28, sp
    sub sp, sp, #2048 
    sub sp, sp, #2048 
    sub sp, sp, #2048
    sub sp, sp, #2048 ; current limit

    sub w0, w0, #1 ; check if we have more argv than filename
    cbnz w0, arg_ok
    mov x0, #0
    b err
arg_ok: ; arguments validated
    ldr x19, [x1, #8]
    mov x20, sp ; reserve 4096 for stack
    add x26, x20, #2048 
    add x26, x26, #2048 ; reserve 2048 for function returns
    add x21, x20, #2048 ; reserve max 1792 or 224 values for definitions (16 bytes each)
    mov x22, #0 ; definition count
    mov x23, #10 ; constant 10 for madd
    mov x24, #0 ; flags
    add x25, x21, #1792; reserve 256 or 32 values for local vars (8 bytes each)
    ; x27 reserved for values surviving function calls

pick: ; main interpreter loop - pick next token
    ldrb w0, [x19], #1
    cbz w0, end
    cmp w0, #0x22
    b.eq str
    cmp w0, #0x28 ; (
    b.eq comm
    cmp w0, #0x3a ; :
    b.eq def_skip_space
    cmp w0, #0x3b ; ;
    b.eq end_exec
    cmp w0, #0x09 ; tab
    b.eq pick
    cmp w0, #0x0a ; nl
    b.eq pick
    cmp w0, #0x20 ; space
    b.eq pick
    cmp w0, #0x39 ; 9
    b.gt wrd
    cmp w0, #0x30 ; 0
    b.lt wrd
    b num
str: ; parse string literal
    mov x1, x19
str_loop:
    ldrb w0, [x19], #1
    cbz w0, str_err
    cmp w0, #0x22 
    b.ne str_loop
    str x1, [x20], #8
    b pick
str_err: ; string not properly terminated
    mov w0, #1
    b err
def_skip_space: ; skip whitespace before definition name
    ldrb w0, [x19], #1
    cmp w0, #0x09
    b.eq def_skip_space
    cmp w0, #0x0a
    b.eq def_skip_space
    cmp w0, #0x20
    b.eq def_skip_space
def: ; define new word
    ; x1 = string ref, x2 = id_len, x3 = total_len
    mov x24, #1
    bl wrd
    mov x3, x2
def_loop:
    ldrb w0, [x19], #1
    add x3, x3, #1
    cbz w0, def_err
    cmp w0, #0x3b
    b.eq def_end
    b def_loop
def_end: ; finalize definition storage
    bfi x2, x3, #16, #48
    add x3, x21, x22, LSL#4
    stp x1, x2, [x3]
    add x22, x22, #1
    b pick
def_err: ; definition not properly terminated
    mov w0, #2
    b err
wrd: ; parse word token
    ; x1 = string ref, x2 = len
    mov x1, x19
    mov w2, #1
    sub x1, x1, #1
wrd_loop:
    ldrb w0, [x19], #1
    cbz w0, wrd_fin
    cmp w0, #0x09
    b.eq wrd_fin
    cmp w0, #0x0a
    b.eq wrd_fin
    cmp w0, #0x20
    b.eq wrd_fin
    cmp w0, #0x22
    b.eq wrd_fin
    cmp w0, #0x28
    b.eq wrd_fin
    add w2, w2, #1
    b wrd_loop
wrd_fin: ; word parsing complete
    tbz x24, #0, wrd_find
    mov x24, #0
    ret
wrd_find: ; lookup word in definitions
    ; x0 = defs num
    sub x19, x19, #1
    mov x0, x22
wrd_find_loop:
    cbz x0, wrd_find_fin_fail
    sub x0, x0, #1
    add x4, x21, x0, LSL#4
    ldp x5, x6, [x4]
    bfi x7, x6, #0, #16
    cmp w2, w7
    b.eq wrd_find_cmp
    b wrd_find_loop
wrd_find_cmp: ; compare word strings
    mov w8, #0
wrd_find_cmp_loop:
    ldrb w10, [x1, x8]
    ldrb w11, [x5, x8]
    add w8, w8, #1
    cmp x10, x11
    b.ne wrd_find_cmp_ret
    cmp w8, w2
    b.eq wrd_find_fin
    b wrd_find_cmp_loop
wrd_find_cmp_ret: ; comparison failed, continue search
    b wrd_find_loop
wrd_find_fin: ; word found in definitions - execute
    mov x24, #2
    add x5, x5, x7
    str x19, [x26], #8
    mov x19, x5
    b pick 
wrd_find_fin_fail: ; word not found - check built-in operations
    cmp w2, #1
    b.eq wrd_find.1
    cmp w2, #2
    b.eq wrd_find.2
    cmp w2, #3
    b.eq wrd_find.3
    cmp w2, #4
    b.eq wrd_find.4
    b wrd_find_err
wrd_find.1: ; single-character built-in operations
    ldrb w0, [x1]
    cmp w0, #0x21 ; !
    b.eq put
    cmp w0, #0x23 ; #
    b.eq res
    cmp w0, #0x24 ; $
    b.eq del
    cmp w0, #0x25 ; %
    b.eq loc
    cmp w0, #0x26 ; &
    b.eq get_arg
    cmp w0, #0x2a ; *
    b.eq mul
    cmp w0, #0x2b ; +
    b.eq add
    cmp w0, #0x2c ; ,
    b.eq input
    cmp w0, #0x2d ; -
    b.eq sub
    cmp w0, #0x2e ; .
    b.eq print
    cmp w0, #0x2f ; /
    b.eq div
    cmp w0, #0x40 ; @
    b.eq get
    b wrd_find_err
wrd_find.2: ; two-character built-in operations
    ldrh w0, [x1]
    mov w2, #0x2162
    cmp w0, w2 ; b!
    b.eq put_b
    mov w2, #0x4062
    cmp w0, w2 ; b@
    b.eq get_b
    mov w2, #0x662c
    cmp w0, w2 ; ,f
    b.eq readf
    mov w2, #0x662e
    cmp w0, w2 ; .f
    b.eq writef
    mov w2, #0x6669
    cmp w0, w2 ; if
    b.eq if
    mov w2, #0x7363
    cmp w0, w2 ; cs
    b.eq to_cs
    mov w2, #0x6373
    cmp w0, w2 ; sc
    b.eq from_cs
    mov w2, #0x2e73
    cmp w0, w2 ; s.
    b.eq print_s
    mov w2, #0x2e78
    cmp w0, w2 ; x.
    b.eq print_x
    mov w2, #0x2626
    cmp w0, w2 ; &&
    b.eq arg_count
    mov w2, #0x6623
    cmp w0, w2 ; #f
    b.eq open
    mov w2, #0x6624
    cmp w0, w2 ; $f
    b.eq close
    b wrd_find_err
wrd_find.3: ; three-character built-in operations
    ldrh w0, [x1]
    mov w2, #0x2c64
    cmp w0, w2 ; s,
    b.eq readf_s
    mov w2, #0x2e64
    cmp w0, w2 ; s.
    b.eq writef_s
    mov w2, #0x7564
    cmp w0, w2 ; du
    b.eq dup
    mov w2, #0x7572
    cmp w0, w2 ; ru
    b.eq run
    b wrd_find_err
wrd_find.4: ; four-character built-in operations
    ldrsw x0, [x1]
    mov w2, #0x7773 ; ws
    movk w2, #0x7061, LSL#16 ; pa
    cmp x0, x2 ; swap
    b.eq swap
    mov w2, #0x7363 ; sc
    movk w2, #0x2e66, LSL#16 ; f.
    cmp x0, x2 ; cs.f
    b.eq writef_cs
    mov w2, #0x7865 ; xe
    movk w2, #0x7469, LSL#16 ; ti
    cmp x0, x2 ; exit
    b.eq exit_wrd
    b wrd_find_err
wrd_find_err: ; word not found error
    mov w0, #3
    b err
comm: ; skip comment until closing paren
    ldrb w0, [x19], #1
    cbz w0, comm_err
    cmp w0, #0x29 ; )
    b.eq pick
    b comm
comm_err: ; comment not properly closed
    mov w0, #4
    b err
num: ; parse numeric literal
    sub x0, x0, #0x30 ; 0
    mov x1, x0
num_loop:
    ldrb w0, [x19], #1
    cmp w0, #0x39 ; 9
    b.gt num_fin
    cmp w0, #0x30 ; 0
    b.lt num_fin
    sub x0, x0, #0x30 ; 0
    madd x1, x1, x23, x0
    b num_loop
num_fin: ; push number onto stack
    sub x19, x19, #1
    str x1, [x20], #8
    b pick
if: ; conditional operation - if x > 0 then y else z
    ldp x0, x1, [x20, #-16]!
    ldr x2, [x20, #-8]!
    cmp x2, #0
    csel x0, x0, x1, gt
    str x0, [x20], #8
    b pick
dup: ; duplicate top of stack
    ldrb w0, [x1, #2]
    cmp w0, #0x70 ; p
    b.ne wrd_find_err
    ldr x0, [x20, #-8]
    str x0, [x20], #8
    b pick
swap: ; swap top two stack values
    ldp x0, x1, [x20, #-16]
    stp x1, x0, [x20, #-16]
    b pick
loc: ; calculate local variable address
    ldr x0, [x20, #-8]
    add x0, x25, x0, LSL#3
    str x0, [x20, #-8]
    b pick
put: ; store quad at address (!)
    ldp x0, x1, [x20, #-16]!
    str x0, [x1]
    b pick
put_b: ; store byte at address (b!)
    ldp x0, x1, [x20, #-16]!
    strb w0, [x1]
    b pick
add: ; addition operation (+)
    ldp x0, x1, [x20, #-16]!
    add x0, x0, x1
    str x0, [x20], #8
    b pick
sub: ; subtraction operation (-)
    ldp x0, x1, [x20, #-16]!
    sub x0, x0, x1
    str x0, [x20], #8
    b pick
mul: ; multiplication operation (*)
    ldp x0, x1, [x20, #-16]!
    mul x0, x0, x1
    str x0, [x20], #8
    b pick
div: ; division operation (/)
    ldp x0, x1, [x20, #-16]!
    sdiv x0, x0, x1
    str x0, [x20], #8
    b pick
get: ; load quad from address (@)
    ldr x0, [x20, #-8]
    ldr x0, [x0]
    str x0, [x20, #-8]
    b pick
get_b: ; load byte from address (b@)
    ldr x0, [x20, #-8]
    ldrb w0, [x0]
    str x0, [x20, #-8]
    b pick
input: ; read 8 bytes from stdin (,)
    bl do_input_quad
    b pick
input_b: ; read 1 byte from stdin
    bl do_input_byte
    b pick
print: ; print number (.)
    adrp x0, pnum@PAGE
    add x0, x0, pnum@PAGEOFF
    bl do_pop_print
    b pick
print_s: ; print string (s.)
    ldr x0, [x20, #-8]
    mov x27, x0
    bl prep_str
    adrp x0, pstr@PAGE
    add x0, x0, pstr@PAGEOFF
    bl do_pop_print
    mov x0, x27
    bl fix_str
    b pick
print_x: ; print hex address (x.)
    adrp x0, pxnum@PAGE
    add x0, x0, pxnum@PAGEOFF
    bl do_pop_print
    b pick
res: ; allocate memory (#)
    ldr x0, [x20, #-8]
    bl _malloc
    str x0, [x20, #-8]
    b pick
del: ; free memory ($)
    ldr x0, [x20, #-8]!
    bl _free
    b pick
open: ; open file (#f)
    ldp x1, x0, [x20, #-16]!
    ldr x2, [x20, #-8]
    mov x27, x0
    bl prep_str
    bl _open
    str x0, [x20, #-8]
    mov x0, x27
    bl fix_str
    b pick
close: ; close file ($f)
    ldr x0, [x20, #-8]!
    bl _close
    b pick
arg_count: ; get argument count (&&)
    ldr x0, [x28]
    str x0, [x20], #8
    b pick
get_arg: ; get command line argument (&)
    ldr x1, [x28, #8]
    cbz x1, get_arg_z
    ldr x0, [x20, #-8]
    ldr x0, [x1, x0, LSL#3]
    str x0, [x20, #-8]
    b pick
get_arg_z: ; null argument case
    str x1, [x20, #-8]
    b pick
run: ; execute string as code (run)
    ldrb w0, [x1, #2]
    cmp w0, #0x6e ; n
    b.ne wrd_find_err
    ldr x1, [x20, #-8]!
    mov x0, x1
    bl cnt_str
    sub w2, w0, #1
    cbz w2, pick
    add x19, x19, #1
    b wrd_find
to_cs: ; convert to C string format (cs)
    ldr x0, [x20, #-8]
    bl fix_str
    b pick
from_cs: ; convert from C string format (sc)
    ldr x0, [x20, #-8]
    bl prep_str
    b pick
writef: ; write to file (.f)
    ldp x1, x0, [x20, #-16]!
    ldr x2, [x20, #-8]!
    bl _write 
    ; returns bytes written
    b pick
writef_cs: ; write C string to file (cs.f)
    ldp x1, x3, [x20, #-16]!
    mov x0, x1
    bl cnt_strnull
    mov x0, x3
    sub x2, x8, x1
    bl _write 
    ; returns bytes written
    b pick
writef_s: ; write string to file (s.f)
    ldrb w0, [x1, #2]
    cmp w0, #0x66 ; f
    b.ne pick
    ldp x1, x3, [x20, #-16]!
    mov x0, x1
    bl prep_str
    mov x0, x3
    sub x2, x8, x1
    bl _write 
    ; returns bytes written
    mov x0, x1
    bl fix_str
    b pick
readf: ; read from file (,f)
    ldp x1, x0, [x20, #-16]!
    ldr x2, [x20, #-8]!
    bl _read 
    ; returns bytes read
    b pick
readf_s: ; read string from file (s,f)
    ldrb w0, [x1, #2]
    cmp w0, #0x66 ; f
    b.ne pick
    ldp x1, x0, [x20, #-16]!
    mov x3, x0
    mov x0, x1
    bl prep_str
    mov x0, x3
    sub x2, x8, x1
    bl _read 
    ; returns bytes read
    mov x0, x1
    bl fix_str
    b pick
exit_wrd: ; exit with code from stack (exit)
    ldr x0, [x20, #-8]
    b exit
end_exec: ; return from word definition (;)
    ldr x19, [x26, #-8]!
    b pick

end: ; normal program termination
    mov w0, #0
    b exit
err: ; error handler - print message and exit
    adrp x1, err_str@PAGE
    add x1, x1, err_str@PAGEOFF
    ldr x0, [x1, x0, LSL#3]
    bl _puts
    mov w0, #1
exit: ; exit program with status code
    add sp, sp, #2048
    add sp, sp, #2048
    add sp, sp, #2048
    add sp, sp, #2048
    add sp, sp, #16
    ldp fp, lr, [sp], #16
    ret

.section STR,"S"
.align 4
pnum: .asciz "%lld\n"
pxnum: .asciz "%p\n"
pstr: .asciz "%s\n"
err_str.0: .asciz "Expected at least one argument"
err_str.1: .asciz "String was not ended"
err_str.2: .asciz "Definition was not ended"
err_str.3: .asciz "No such word was found"
err_str.4: .asciz "Comment was not ended"

.section REF,""
.align 4
err_str: 
    .quad err_str.0
    .quad err_str.1
    .quad err_str.2
    .quad err_str.3
    .quad err_str.4
